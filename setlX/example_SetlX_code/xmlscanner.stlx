// This program demonstrates the scan statement

// using an extremely limited XML example
str := "<abc/>< def foo=\"bar\" />";

tokens := [];

// create token list
scan (str) {
    regex '<' :
        tokens += [OpenTag()];
    regex '/>' :
        tokens += [CloseTag()];
    regex '\w+' -> [id]:
        tokens += [Id(id)];
    regex '=':
        tokens += [Assign()];
    regex '"(\S+)"' -> [_, text]:
        tokens += [String(text)];
    regex '\s+':
        // skip whitespace
    regex '.' -> x:
        // match anything unknown
        print("unrecognized character: $x[1]$");
    default :
        print("xml-scanner-error!");
}

//print(tokens);

// recreate input from token list
while (#tokens > 0) {
    match(tokens) {
        // match normal tag
        case [OpenTag(), Id(id), CloseTag() | rest]:
            print("<$id$/>");
            tokens := rest;
        // tag with exactly one attribute
        case [OpenTag(), Id(id), Id(id2), Assign(), String(text), CloseTag() | rest]:
            print("<$id$ $id2$=\"$text$\"/>");
            tokens := rest;
        default:
            print("xml-parser-error!");
            break;
    }
}


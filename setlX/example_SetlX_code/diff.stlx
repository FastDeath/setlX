// differentiate the term t with respect to the variable x
diff := procedure(t, x) {
    match (t) {
        case t1 + t2 :
            return diff(t1, x) + diff(t2, x);
        case t1 - t2 :
            return diff(t1, x) - diff(t2, x);
        case t1 * t2 :
            return diff(t1, x) * t2 + t1 * diff(t2, x);
        case t1 / t2 :
            return ( diff(t1, x) * t2 - t1 * diff(t2, x) ) / t2 * t2;
        case f ** g :
            return diff( @exp(g * @ln(f)), x);
        case ln(a) :
            return diff(a, x) / a;
        case exp(a) :
            return diff(a, x) * @exp(a);
        case (t) :
            return diff(t, x);
        case ^variable(x) : // x is defined above, therefore must be equal to match
            return 1;
        case ^variable(y) : // y is undefined, therefore matches any other variable
            return 0;
        default:
            if (isInteger(t) || isReal(t)) {
                return 0;
            } else {
                abort("error");
            }
    }
};

test := procedure(s) {
    t := parse(s);
    print("testing $t$:");
    d := diff(t, "x");
    print(d, "\n");
};

test("x");
test("y");
test("1");
test("x+x");
test("x+x+y");
test("x*x");
test("x/x");
test("1/x");
test("ln(x)");
test("exp(x)");
test("x ** x");


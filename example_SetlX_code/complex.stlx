class complex(real, imag) {
    mReal := real;
    mImag := imag;
    
  static {
    f_str := [] |-> this.mReal + " + " + this.mImag + " * i";

    sum := 
        that |-> complex(this.mReal + that.mReal, 
                                 this.mImag + that.mImag);
    difference := 
        that |-> complex(mReal - that.mReal, 
                                 mImag - that.mImag);
    product := procedure(that) {
        return complex(mReal * that.mReal - mImag * that.mImag,
                       mReal * that.mImag + mImag * that.mReal);
    };
    quotient := procedure(that) {
        denom := that.mReal * that.mReal + this.mImag * this.mImag;
        real  := (mReal * that.mReal + mImag * that.mImag) / denom;
        imag  := (mImag * that.mReal - mReal * that.mImag) / denom;
        return complex(real, imag);
    };
    power := procedure(that) {
        return exp(that * log(this));
    };
    f_exp := procedure() {
        r := exp(this.mReal);
        return complex(r * cos(this.mImag), r * sin(this.mImag));
    };
    f_log := procedure() {
        r := log(this.mReal * this.mReal + this.mImag * this.mImag) / 2; 
        return complex(r, atan2(this.mImag, this.mReal));
    };
  }
}

x := complex(3,  4);
y := complex(3, -4);

print("$x$ + $y$ = $x + y$");
print("$x$ - $y$ = $x - y$");
print("($x$) * ($y$) = $x * y$");
print("($x$) / ($y$) = $x / y$");
print("($x$) ** ($y$) = $x ** y$");

phi := complex(2/3, 1/2);
for (i in [1..10]) {
    n := complex(i, 0);
    z := phi ** n;
    print(z - exp(log(z)));
}

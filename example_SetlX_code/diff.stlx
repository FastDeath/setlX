// differentiate the term t with respect to the variable x
diff := procedure(t, x) {
    match (t) {
        case a + b :
            return diff(a, x) + diff(b, x);
        case a - b :
            return diff(a, x) - diff(b, x);
        case a * b :
            return diff(a, x) * b + a * diff(b, x);
        case a / b :
            return ( diff(a, x) * b - a * diff(b, x) ) / b * b;
        case a ** b :
            return diff( @exp(b * @ln(a)), x);
        case fn(a) :
            switch {
                case fn == parse("ln"):
                    return diff(a, x) / a;
                case fn == parse("exp"):
                    return diff(a, x) * @exp(a);
                default:
                    abort("unknown function");
            }
        case a | isNumber(a) :
            return 0;
        case ^variable(id) | id == str(x):
            return 1;
        case ^variable(id):
            return 0;
        default:
            abort("error: $t$");
    }
};

test := procedure(s) {
    t := parse(s);
    print("testing $t$:");
    d := diff(t, parse("x"));
    print(d, "\n");
};

test("x");
test("y");
test("1");
test("x+x");
test("x+x+y");
test("x*x");
test("x/x");
test("1/x");
test("ln(x)");
test("exp(x)");
test("x ** x");

